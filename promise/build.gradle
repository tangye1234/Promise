apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion 22
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 23
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

def versionName = project.hasProperty('version') ? project.property('version') : ''
def artifactId = project.hasProperty('artifactId') ? project.property('artifactId') : ''
def groupId = project.hasProperty('groupId') ? project.property('groupId') : ''
def repoUrl = project.hasProperty('repoUrl') ? project.property('repoUrl') : ''
def username = project.hasProperty('username') ? project.property('username') : ''
def password = project.hasProperty('password') ? project.property('password') : ''

uploadArchives {
    repositories {
        mavenDeployer {
            pom.groupId = groupId
            pom.artifactId = artifactId
            pom.version = versionName
            pom.project {
                name 'Android Promise Library'
                packaging 'aar'
                description 'Promise library for Android applications'
                inceptionYear '2015'
            }
            repository(url: repoUrl) {
                authentication(userName: username, password: password)
            }
            uniqueVersion=true
        }
    }
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
}

task androidJavadocsJar(type: Jar) {
    classifier = 'javadoc'
    baseName = artifactId
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    baseName = artifactId
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidJavadocsJar
    archives androidSourcesJar
}